spending vesting

struct Datum {
    sponsor: PubKeyHash
    payee: PubKeyHash
    deadline: Time
}

enum Redeemer {
    Cancel
    Claim
}

func main(datum: Datum, redeemer: Redeemer, context: ScriptContext) -> Bool {
    tx: Tx = context.tx;
    now: Time = tx.time_range.start;
    print("current_time_is:");
    print(now.show());
    redeemer.switch {
        Cancel => {
            // Check if deadline hasn't passed
            (now < datum.deadline).trace("VS1: ") && 

            // Check that the owner signed the transaction
            tx.is_signed_by(datum.sponsor).trace("VS2: ")
        },
        Claim => {
           // Check if deadline has passed.
           (now > datum.deadline).trace("VS3: ") &&

           // Check that the beneficiary signed the transaction.
           tx.is_signed_by(datum.payee).trace("VS4: ")
        }
    }
}
